.org $0e02

.alias program_start $1900
.alias rstack_top $9f
.alias stack_top $8f

; Programs including this file must define:
; program_start : The start address of the bytecodes in memory.
; stack_top     : The address in zero page of the top byte in the parameter stack.
; rstack_top    : The address in zero page of the top byte in the return address stack.

; Program counter
.alias pc $70
.alias pc_low $70
.alias pc_high $71
; Variable/parameter stack
.alias sp $72
; Return address stack
.alias rsp $73
; Carry/borrow
.alias cb $74
.alias dest $75
.alias first $76
.alias second $77
.alias extra $78

process:

    lda #0
    sta cb

    ; Reserve memory for variables and a return address stack.
    lda #rstack_top
    sta rsp
    lda #stack_top
    sta sp

    lda #<program_start
    sta pc_low
    lda #>program_start
    sta pc_high

    next_instruction:
    ldy #0
    lda (pc),y
    and #$0f                ; Extract the instruction number.
    tax
    lda lookup_high,x       ; Load the high byte of the routine address.
    pha                     ; Store the address in a fixed address.
    lda lookup_low,x        ; Load the low byte of the routine address.
    pha                     ; Store the address in a fixed address.

    ; Jump to the address held by the temporary address. Instructions will
    ; jump back to the next_instruction label to continue execution.
    rts

lsr_4:
    lsr
    lsr
    lsr
    lsr
    clc
    rts

inc_pc:
    inc pc
    bne +
    inc [pc + 1]
*   rts

next_byte:
    jsr inc_pc
    ldy #0
    lda (pc),y
    rts

split_args:
    sta first
    jsr lsr_4
    sta second
    lda first
    and #$0f
    sta first
    rts

load_first_second:
    ldy second
    lda (sp),y
    sta second
    ldy first
    lda (sp),y
    sta first
    rts

cond_set_cb:
    ; store C in cb
    lda #0
    rol
    sta cb
    rts

store_dest:
    ldy dest
    sta (sp),y
    rts

inst_lc:    ; A=opcode [dest|opcode value]

    jsr lsr_4       ; dest
    sta dest
    jsr next_byte   ; value
    ldy dest
    sta (sp),y
    jsr inc_pc
    jmp next_instruction

inst_cpy:   ; A=opcode [dest|opcode shift|src]

    jsr lsr_4       ; dest
    sta dest
    jsr next_byte
    jsr split_args  ; A=first=src, second=shift
    tay             ; Y=first
    lda second      ; shift
    cmp #8
    bcc cpy_shift_lt_8
        clc
        adc #248    ; 8-15 -> 248-255 (-8 to -1)
        tax         ; X=shift
        lda (sp),y  ; value
        cpy_asl_loop:
            asl
            inx
            bne cpy_asl_loop
        beq cpy_shift_copy
    cpy_shift_lt_8:
        tax         ; X=shift
        lda (sp),y  ; value
        cpy_lsr_loop:
            lsr
            dex
            bne cpy_lsr_loop
    cpy_shift_copy:
    clc
    ldy dest
    sta (sp),y
    jsr inc_pc
    jmp next_instruction

inst_add:   ; A=opcode [dest|opcode second|first]

    jsr lsr_4       ; dest
    sta dest
    jsr next_byte
    jsr split_args
    jsr load_first_second   ; A=first
    clc
    adc second      ; A = first + second
    jsr cond_set_cb
    jsr store_dest
    jsr inc_pc
    jmp next_instruction

inst_sub:   ; A=opcode [dest|opcode second|first]

    jsr lsr_4       ; dest
    sta dest
    jsr next_byte
    jsr split_args
    jsr load_first_second   ; A=first
    sec
    lda first
    sbc second      ; A = first + second
    jsr cond_set_cb ; inverted?
    jsr store_dest
    jsr inc_pc
    jmp next_instruction

inst_adc:   ; A=opcode [dest|opcode]
    lda cb
    beq inst_adc_ret

        jsr lsr_4       ; dest
        sta dest
        tay
        lda (sp),y
        adc #1
        jsr cond_set_cb
        jsr store_dest

    inst_adc_ret:
    jsr inc_pc
    jmp next_instruction

inst_sbc:   ; A=opcode [dest|opcode]
    lda cb
    beq inst_sbc_ret

        jsr lsr_4       ; dest
        sta dest
        tay
        lda (sp),y
        sec
        sbc #1
        jsr cond_set_cb ; inverted?
        ldy dest
        sta (sp),y

    inst_sbc_ret:
    jsr inc_pc
    jmp next_instruction

inst_and:   ; A=opcode [dest|opcode second|first]

    jsr lsr_4       ; dest
    sta dest
    jsr next_byte
    jsr split_args
    jsr load_first_second   ; A=first
    and second
    jsr store_dest
    jsr inc_pc
    jmp next_instruction

inst_or:    ; A=opcode [dest|opcode second|first]

    jsr lsr_4       ; dest
    sta dest
    jsr next_byte
    jsr split_args
    jsr load_first_second   ; A=first
    ora second
    jsr store_dest
    jsr inc_pc
    jmp next_instruction

inst_xor:   ; A=opcode [dest|opcode second|first]

    jsr lsr_4       ; dest
    sta dest
    jsr next_byte
    jsr split_args
    jsr load_first_second   ; A=first
    eor second
    jsr store_dest
    jsr inc_pc
    jmp next_instruction

inst_ld:    ; A=opcode [dest|opcode high|low]

    jsr lsr_4       ; dest
    sta dest
    jsr next_byte
    jsr split_args
    jsr load_first_second   ; first=low, second=high
    ldy #0
    lda (first),y   ; load the contents of the address
    jsr store_dest
    jsr inc_pc
    jmp next_instruction

inst_st:    ; A=opcode [src|opcode high|low]

    jsr lsr_4       ; src=dest
    sta dest
    jsr next_byte
    jsr split_args
    jsr load_first_second   ; first=low, second=high
    ldy dest
    lda (sp),y
    ldy #0
    sta (first),y
    jsr inc_pc
    jmp next_instruction

.alias offset dest
.alias cond extra

; Map from 6502 processor flags (--ZC) to cond values with bits (-GEL),
; ignoring signed comparisons.
cond_flags:
;     -- -C Z- ZC
.byte 1, 4, 0, 6
;     lt gt -  eq

; The inst_bx routine implements conditional branches (beq, bge, bgt, ble,
; blt, bne) and unconditional branches (b), as well as the not instruction.

inst_bx:    ; A=opcode [cond|opcode offset second|first] (b*)
            ;          [7|opcode offset] (b)
            ;          [0|opcode src|dest] (not)

    jsr lsr_4
    cmp #0
    bne inst_not    ; If cond == 0 this is a not instruction.
    sta cond

    ldy #1
    lda (pc),y
    sta offset

    lda cond
    cmp #7
    bcs inst_bx_branch

    jsr next_byte       ; read pc + 2 (first and second operands)
    jsr split_args
    jsr load_first_second
    cmp second
    php
    pla                 ; N-----ZC
    and #$03            ; Only examine Z and C flags.
    tax
    lda cond_flags,x    ; Obtain the corresponding cond bits.
    and cond            ; Compare with the instruction's cond bits.

    bne inst_bx_branch  ; Branch if a match occurred.

inst_bx_cond_fail:
    jsr inc_pc
    jmp next_instruction

inst_bx_branch:
    clc
    lda offset
    adc pc_low
    sta pc_low
    bcc inst_bx_ret
    inc pc_high

inst_bx_ret:
    jmp next_instruction

inst_not:   ; [src|dest] (not)
    jsr next_byte
    jsr split_args
    jsr load_first_second   ; A=dest=first
    tay
    lda second
    eor #255
    sta (sp),y
    jsr inc_pc
    jmp next_instruction

.alias args dest
.alias low first
.alias high second

inst_js:    ; A=opcode [args|opcode low high]

    jsr lsr_4
    sta args
    jsr next_byte
    sta low
    jsr next_byte
    sta high
    jsr inc_pc
    jsr push_pc
    jsr push_args
    lda low
    sta pc_low
    lda high
    sta pc_high
    rts

push_pc:
    lda pc_high
    sta rsp
    dec rsp
    lda pc_low
    dec rsp
    rts

push_args:
    sec
    lda sp
    sbc args
    sta sp
    clc
    rts

inst_jss:   ; A=opcode [args|opcode offset]

    jsr lsr_4
    sta args
    jsr next_byte
    sta offset
    jsr inc_pc
    jsr push_pc
    jsr push_args
    jmp inst_bx_branch

inst_ret:   ; A=opcode [args|opcode]

    jsr lsr_4   ; args
    adc sp
    sta sp
    clc
    inc rsp
    lda rsp
    sta pc_low
    inc rsp
    lda rsp
    sta pc_high
    jmp next_instruction

inst_sys:   ; A=opcode [value|opcode]

    jsr lsr_4
    cmp #0      ; exit
    beq exit
    jsr inc_pc
    jmp next_instruction

exit:
    lda #42
    jsr $ffee
    rts

lookup_low:
.byte <[inst_lc - 1]
.byte <[inst_cpy - 1]
.byte <[inst_add - 1]
.byte <[inst_sub - 1]
.byte <[inst_and - 1]
.byte <[inst_or - 1]
.byte <[inst_xor - 1]
.byte <[inst_ld - 1]
.byte <[inst_st - 1]
.byte <[inst_bx - 1]
.byte <[inst_adc - 1]
.byte <[inst_sbc - 1]
.byte <[inst_js - 1]
.byte <[inst_jss - 1]
.byte <[inst_ret - 1]
.byte <[inst_sys - 1]

lookup_high:
.byte >[inst_lc - 1]
.byte >[inst_cpy - 1]
.byte >[inst_add - 1]
.byte >[inst_sub - 1]
.byte >[inst_and - 1]
.byte >[inst_or - 1]
.byte >[inst_xor - 1]
.byte >[inst_ld - 1]
.byte >[inst_st - 1]
.byte >[inst_bx - 1]
.byte >[inst_adc - 1]
.byte >[inst_sbc - 1]
.byte >[inst_js - 1]
.byte >[inst_jss - 1]
.byte >[inst_ret - 1]
.byte >[inst_sys - 1]
